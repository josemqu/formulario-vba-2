Option Explicit

Private Sub Worksheet_Change(ByVal Target As Range)
    On Error GoTo fin
    If Target Is Nothing Then Exit Sub

    ' Tomar columnas afectadas en esta edicion
    Dim cols As Object
    Set cols = CreateObject("Scripting.Dictionary")

    Dim c As Range
    For Each c In Target.Columns
        cols(c.Column) = True
    Next c

    Application.EnableEvents = False

    Dim colNum As Variant
    For Each colNum In cols.Keys
        ' Leer encabezado en fila 1 de esa columna como nombre del catalogo
        Dim header As String
        header = CStr(Me.Cells(1, CLng(colNum)).value)

        If LenB(header) > 0 Then
            ' Obtener letra de columna (por ejemplo: "A", "BC", etc.)
            Dim colLetter As String
            colLetter = Split(Me.Cells(1, CLng(colNum)).Address(False, False), CStr(Me.Cells(1, CLng(colNum)).Row))(0)
            ' Actualizar rango y nombres (minusculas y mayusculas)
            modSetup.EnsureCatalog Me, colLetter, header
        End If
    Next colNum

fin:
    On Error Resume Next
    Application.EnableEvents = True
End Sub

Public Sub RebuildCatalogNames()
    ' Recorre todas las columnas con encabezado en fila 1 y regenera los nombres
    Dim lastCol As Long, col As Long
    Application.ScreenUpdating = False
    For col = 1 To Me.Cells(1, Me.Columns.Count).End(xlToLeft).Column
        Dim header As String
        header = CStr(Me.Cells(1, col).value)
        If LenB(header) > 0 Then
            Dim colLetter As String
            colLetter = Split(Me.Cells(1, col).Address(False, False), CStr(Me.Cells(1, col).Row))(0)
            modSetup.EnsureCatalog Me, colLetter, header
        End If
    Next col
    Application.ScreenUpdating = True
End Sub